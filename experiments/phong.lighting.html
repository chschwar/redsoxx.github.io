<html>
<head>

    <!-- 
    Blinn â€“ Phong model uses the concept of Halfway vector(H) 
    between Light source (L) and View direction (V) instead of Reflection(R).
    http://in2gpu.com/2014/06/19/lighting-vertex-fragment-shader/
    http://learningwebgl.com/blog/?p=684
    http://www.tomdalling.com/blog/modern-opengl/08-even-more-lighting-directional-lights-spotlights-multiple-lights/
    -->
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8">
    <title>Phong lighting</title>
    
    <script id="vertex-shader" type="x-shader/x-vertex">
    attribute vec4 vPosition;
    attribute vec3 vNormal;
    attribute vec2 vUv;
    
    uniform mat4 ml_matrix;
    uniform mat4 pr_matrix;
    uniform mat4 vw_matrix;
    uniform mat3 normal_matrix;
    
    varying vec3 fNormal_world;
    varying vec3 fPosition_eye;
    varying vec3 fPosition_world;
    
    void main() 
    {
        mat4 mlvw_matrix = vw_matrix * ml_matrix;       // model - view matrix
        vec4 position_eye = mlvw_matrix * vPosition;    // position in eye space
        fPosition_world = (ml_matrix * vPosition).xyz;  // position in world space
        gl_Position = pr_matrix * position_eye;         // position in clip space
        fNormal_world = normalize(normal_matrix * vNormal);
        fPosition_eye = position_eye.xyz/position_eye.w; // (Note: eyeCoords.w is 1 unless modelview is weird)
    }
    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">   
    #ifdef GL_FRAGMENT_PRECISION_HIGH
       precision highp float;
    #else
       precision mediump float;
    #endif
    
    const float PI = 3.14;
    
    uniform vec4 uLightPosition_world;
    uniform vec4 uLightDirection_world;
    
    uniform vec4 uCameraPosition_world;
    
    uniform mat4 ml_matrix;
    uniform mat4 pr_matrix;
    uniform mat4 vw_matrix;
    uniform mat3 normal_matrix;
    
    uniform vec4 ambientProduct, diffuseProduct, specularProduct;
    uniform float materialShininess;
    varying vec3 fNormal_world;
    
    uniform bool uBlinn;
    uniform int uLightType;

    varying vec3 fPosition_eye;
    varying vec3 fPosition_world;
    
    float influence(vec3 normal, float coneAngle)
    {
        float minConeAngle = ((360.0-coneAngle-10.0)/360.0)*PI;
        float maxConeAngle = ((360.0-coneAngle)/360.0)*PI;
        return smoothstep(minConeAngle, maxConeAngle, acos(normal.z));
    }
    

    float blinn(vec3 L, vec3 V, vec3 N, float materialShininess)
    {
        // blinn modification - half way in eye coordinates
        vec3 H  = normalize( L + V );
        float Ks = pow( max(dot(N, H), 0.0), materialShininess );
        
        return Ks;
    }
    
    float phong(vec3 L, vec3 V, vec3 N, float materialShininess)
    {
        vec3 R = normalize(reflect(-L, N));
        float Ks = pow(max(dot(R, V), 0.0), materialShininess);
        
        return Ks;
    }
    
    
    void main()
    {
        /*
        L  - Light source vector (direction) striking the surface
        (abbreviated to 'L', but remember this is a vector).

        V  - Viewpoint vector (eyepoint) looking at the surface
        (abbreviated to 'V', but remember this is a vector).

        N  - Normal vector (perpendicular) to the surface
        (abbreviated to 'N', but remember this is a vector).
        */

        // light direction 
        vec3 L = normalize( uLightPosition_world.xyz/uLightPosition_world.w - fPosition_world );
        
        // view direction (direction to eye)
        vec3 V = normalize( uCameraPosition_world.xyz/uCameraPosition_world.w - fPosition_world );
        
        // normals in world coordinates
        vec3 N = fNormal_world;        
        
        // Compute terms in the illumination equation
        vec4 ambient = ambientProduct;
        
        float spotlightFactor = 1.0;
        
        // point light
        if (1 == uLightType)
        {
            if(uLightPosition_world.w == 0.0)  
            {
                // light direction
                L = -normalize( uLightPosition_world.xyz);
            }
            else
            {
                // light direction
                L = normalize( uLightPosition_world.xyz/uLightPosition_world.w - fPosition_world );
            }    
        }
        
        // directional light
        if (2 == uLightType)
        {
            // light direction
            L = -normalize(uLightDirection_world.xyz);
        }
        if (3 == uLightType)
        {
            /* Spotlight */
            vec3 spot_direction = normalize (-uLightDirection_world.xyz);
            float spot_arc = 1.0 - 5.0 / 90.0;
            float spot_dot = dot (spot_direction, L);
            /*float spot_factor = 1.0;
            if (spot_dot < spot_arc) {
                spot_factor = 0.0;
            }*/
            
            spotlightFactor = (spot_dot - spot_arc) / (1.0 - spot_arc);
            spotlightFactor = clamp (spotlightFactor, 0.1, 1.0);
            //spotlightFactor = influence(L,55.0);
        }
        
        // only ambient light
        if (0 == uLightType)
        {
            gl_FragColor = ambient;
            gl_FragColor.a = 1.0;
        }
        else
        {
            float Kd = max( dot(L, N), 0.0 );
            if (Kd > 1.0)
                Kd = 1.0;
            vec4 diffuse = Kd * diffuseProduct * spotlightFactor;
            
            float Ks = blinn(L, V, N, materialShininess);
            vec4 specular = Ks * specularProduct * spotlightFactor;
            
            if( dot(L, N) < 0.0 ) 
            {
                specular = vec4(0.0, 0.0, 0.0, 1.0);
            }
            
            gl_FragColor = ambient + diffuse + specular;
            gl_FragColor.a = 1.0;
        }
    }
    </script>
    
    <script type="text/javascript" src="../common/webgl-utils.js"></script>
    <script type="text/javascript" src="../common/initShaders.js"></script>
    <script type="text/javascript" src="../common/MV.js"></script>
    <script type="text/javascript" src="phong.lighting.js"></script>
    <script type="text/javascript" src="../lighting/camera.js"></script>
    <script type="text/javascript" src="../lighting/directionallight.js"></script>
    <script type="text/javascript" src="../lighting/pointlight.js"></script>
    <script type="text/javascript" src="../lighting/spotlight.js"></script>
    <script type="text/javascript" src="../primitives/sphere.js"></script>
    <script type="text/javascript" src="../primitives/cone.js"></script>
    <script type="text/javascript" src="../primitives/plane.js"></script>
    <script type="text/javascript" src="../primitives/cylinder.js"></script>
    <script type="text/javascript" src="../primitives/cube.js"></script>
    <script type="text/javascript" src="../primitives/torus.js"></script>
    <script type="text/javascript" src="../common/webgl-debug.js"></script>
</head>

<body>
    <table style="width: 1200px;" border="0" cellspacing="1" cellpadding="1">
    <tbody>
    <tr>
    <td valign="top" rowspan="25"><canvas id="gl-canvas" width="900" height="900"> Oops ... your browser doesn't support the HTML5 canvas element </canvas></td>
    <td>
    <label><b>Light type:</b> <br><select id="lighttype">
        <option value="0">Ambient Light</option>
        <option value="1">Point Light</option>
        <option value="2">Directional Light</option>
        <option value="3">Spot Light</option>        
    </select></label><br>
    </td>
    </tr>    
</body>
</html>